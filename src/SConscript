# This file tells scons what files make up SWEGUI

import sys
import os

Import('env_program')

# List sources
sourceFiles = ['main.cpp', 'GuiApplication.cpp', 'GuiApplicationWindow.cpp', 'probe/DataProbe.cpp', 'probe/ProbeColumns.cpp']
sourceFiles += ['renderer/DataRenderer.cpp', 'renderer/NetCdfImageStream.cpp', 'renderer/Layer.cpp', 'reader/NetCdfReader.cpp']
for i in sourceFiles: env_program.src_files.append(env_program.Object(i))

# Setup
env_program.Append(LIBS=[env_program.library_name], LIBPATH=['../build_'+env_program.library_name])
env_program.Append(LINKFLAGS=Split('-z origin') )
env_program.Append(RPATH=env_program.Literal('\\$$ORIGIN/lib'))
env_program.Append(CCFLAGS=['-fPIC'])

# Add search path for swe
Repository('#submodules/swe')
env_program.Append(CPPPATH=['#submodules/swe/src'])

# Build binary
env_program.Program(env_program.program_name, env_program.src_files)

# Define tests
env_program.CxxTest('SWEGUITests', ['unit_tests/SWEGUITests.t.h', 'renderer/NetCdfImageStream.cpp', 'renderer/Layer.cpp', 'reader/NetCdfReader.cpp'])

# Copy binary
Install('#'+env_program['buildDir']+'/packet', env_program.program_name)

# Copy UI scripts
uis = ['main.glade', 'main.frag', 'crosshair.png', 'crosshair_active.png']
uis += ['icon_fancy.png', 'icon_32.png']
for x in uis: Install('#'+env_program['buildDir']+'/packet/ui', 'ui/'+x)